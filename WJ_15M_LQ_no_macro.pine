//@version=6
indicator("WJ 15M LQ", overlay=true, max_lines_count=500, max_labels_count=500, max_bars_back=5000)

// Set max_bars_back for important series
max_bars_back(time, 5000)
max_bars_back(close, 5000)
max_bars_back(open, 5000)

htf = input.timeframe("15", "Higher Timeframe", options=["1", "3", "5", "15", "30", "45", "60", "120", "180", "240", "D", "W", "M"])
ltf = input.timeframe("1", "Lower Timeframe", options=["1", "3", "5", "15", "30", "45", "60", "120", "180", "240", "D"])

plotPivots = input.bool(true, title='Plot Pivots', group="Pivot Lines")
pivotLength = input.int(500, "Line Extension (bars)", minval=1, maxval=500, group="Pivot Lines")
lineWidth = input.int(1, "Line Width", minval=1, maxval=4, group="Pivot Lines")
terminationType = input.string("Wicks", "Line Termination Type", options=["Wicks", "Body"], group="Pivot Lines")
showHighPivots = input.bool(true, "Show High Pivots", group="High Pivots")
highColor = input.color(color.red, "High Pivot Color", group="High Pivots")
highStyle = input.string("Dashed", title='High Pivot Style', options=["Solid", "Dotted", "Dashed"], group="High Pivots")
highExtend = input.string("None", title="High Pivot Extension", options=["None", "Right", "Left", "Both"], group="High Pivots")
showLowPivots = input.bool(true, "Show Low Pivots", group="Low Pivots")
lowColor = input.color(color.blue, "Low Pivot Color", group="Low Pivots")
lowStyle = input.string("Dashed", title='Low Pivot Style', options=["Solid", "Dotted", "Dashed"], group="Low Pivots")
lowExtend = input.string("None", title="Low Pivot Extension", options=["None", "Right", "Left", "Both"], group="Low Pivots")
maxPivots = input.int(500, title='Maximum Pivots', minval=1, maxval=500, group="Pivot Lines")
showLabels = input.bool(true, "Show Labels", group="Pivot Lines")

extendType(type) =>
    switch type
        "None" => extend.none
        "Right" => extend.right
        "Left" => extend.left
        "Both" => extend.both

styleType(type) =>
    switch type
        "Solid" => line.style_solid
        "Dotted" => line.style_dotted
        "Dashed" => line.style_dashed

var highExtendType = extendType(highExtend)
var lowExtendType = extendType(lowExtend)
var highStyleType = styleType(highStyle)
var lowStyleType = styleType(lowStyle)

var array<line> highLines = array.new<line>()
var array<line> lowLines = array.new<line>()
var array<float> highPivots = array.new<float>()
var array<float> lowPivots = array.new<float>()
var array<label> highLabels = array.new<label>()
var array<label> lowLabels = array.new<label>()
var array<bool> highBroken = array.new<bool>()
var array<bool> lowBroken = array.new<bool>()
var int lastPivotTime = 0

var float[] lastHTFHighs = array.new<float>()
var int[] lastHTFHighBars = array.new<int>()
var float[] lastHTFLows = array.new<float>()
var int[] lastHTFLowBars = array.new<int>()

// Simplified pivot detection without macro filtering
getPivots() =>
    float ph = ta.pivothigh(high, 1, 1)
    float pl = ta.pivotlow(low, 1, 1)
    int t = time[1]
    
    [ph, pl, t]

// Get HTF pivots using request.security
[htfPivotHigh, htfPivotLow, pivotTime] = request.security(syminfo.tickerid, htf, getPivots(), gaps=barmerge.gaps_off, lookahead=barmerge.lookahead_off)

// Store current timeframe pivots for reference
ltfPivotHigh = ta.pivothigh(high, 1, 1)
ltfPivotLow = ta.pivotlow(low, 1, 1)

if not na(ltfPivotHigh)
    array.push(lastHTFHighs, ltfPivotHigh)
    array.push(lastHTFHighBars, bar_index - 1)

if not na(ltfPivotLow)
    array.push(lastHTFLows, ltfPivotLow)
    array.push(lastHTFLowBars, bar_index - 1)

var int maxArraySize = 15

// Calculate maxArraySize based on timeframe ratios
if htf == "15"
    maxArraySize := 15
if htf == "30"
    maxArraySize := 30
if htf == "60"
    maxArraySize := 60
if htf == "240"
    maxArraySize := 240
if htf == "D"
    maxArraySize := 1440
if htf == "W"
    maxArraySize := 7200

// Adjust array size based on lower timeframe
if timeframe.period == "1"
    maxArraySize := maxArraySize * 15
if timeframe.period == "5"
    maxArraySize := maxArraySize * 5
if timeframe.period == "15"
    maxArraySize := maxArraySize * (htf == "240" ? 16 : 3)  // 16 periods for 240min/15min ratio

if array.size(lastHTFHighs) > maxArraySize
    array.shift(lastHTFHighs)
    array.shift(lastHTFHighBars)
if array.size(lastHTFLows) > maxArraySize
    array.shift(lastHTFLows)
    array.shift(lastHTFLowBars)

lineBroken(float price, bool isHigh) =>
    if terminationType == "Wicks"
        isHigh ? (high >= price) : (low <= price)
    else
        isHigh ? (math.max(open, close) >= price) : (math.min(open, close) <= price)

// For HTF timeframe
if timeframe.period == htf
    if not na(htfPivotHigh) and pivotTime != lastPivotTime and showHighPivots
        lineH = line.new(bar_index - 1, htfPivotHigh, bar_index + math.min(pivotLength, 500), htfPivotHigh, color=highColor, width=lineWidth, style=highStyleType, extend=highExtendType)
        if showLabels
            labelH = label.new(bar_index - 1, htfPivotHigh + (10 * syminfo.mintick), "Buy Side Liquidity", color=color.new(color.black, 100), textcolor=color.white, style=label.style_label_left, xloc=xloc.bar_index)
            array.push(highLabels, labelH)
        array.push(highLines, lineH)
        array.push(highPivots, htfPivotHigh)
        array.push(highBroken, false)

    if not na(htfPivotLow) and pivotTime != lastPivotTime and showLowPivots
        lineL = line.new(bar_index - 1, htfPivotLow, bar_index + math.min(pivotLength, 500), htfPivotLow, color=lowColor, width=lineWidth, style=lowStyleType, extend=lowExtendType)
        if showLabels
            labelL = label.new(bar_index - 1, htfPivotLow - (15 * syminfo.mintick), "Sell Side Liquidity", color=color.new(color.black, 100), textcolor=color.white, style=label.style_label_left, xloc=xloc.bar_index)
            array.push(lowLabels, labelL)
        array.push(lowLines, lineL)
        array.push(lowPivots, htfPivotLow)
        array.push(lowBroken, false)

// For other timeframes
else
    if not na(htfPivotHigh) and pivotTime != lastPivotTime and showHighPivots
        startBar = bar_index - 1
        if array.size(lastHTFHighs) > 0
            for i = array.size(lastHTFHighs) - 1 to 0
                if array.get(lastHTFHighs, i) == htfPivotHigh
                    startBar = math.max(array.get(lastHTFHighBars, i), bar_index - 4999)
                    break
        lineH = line.new(startBar, htfPivotHigh, bar_index + math.min(pivotLength, 500), htfPivotHigh, color=highColor, width=lineWidth, style=highStyleType, extend=highExtendType)
        if showLabels
            labelH = label.new(startBar, htfPivotHigh + (10 * syminfo.mintick), "Buy Side Liquidity", color=color.new(color.black, 100), textcolor=color.white, style=label.style_label_left, xloc=xloc.bar_index)
            array.push(highLabels, labelH)
        array.push(highLines, lineH)
        array.push(highPivots, htfPivotHigh)
        array.push(highBroken, false)

    if not na(htfPivotLow) and pivotTime != lastPivotTime and showLowPivots
        startBar = bar_index - 1
        if array.size(lastHTFLows) > 0
            for i = array.size(lastHTFLows) - 1 to 0
                if array.get(lastHTFLows, i) == htfPivotLow
                    startBar = math.max(array.get(lastHTFLowBars, i), bar_index - 4999)
                    break
        lineL = line.new(startBar, htfPivotLow, bar_index + math.min(pivotLength, 500), htfPivotLow, color=lowColor, width=lineWidth, style=lowStyleType, extend=lowExtendType)
        if showLabels
            labelL = label.new(startBar, htfPivotLow - (15* syminfo.mintick), "Sell Side Liquidity", color=color.new(color.black, 100), textcolor=color.white, style=label.style_label_left, xloc=xloc.bar_index)
            array.push(lowLabels, labelL)
        array.push(lowLines, lineL)
        array.push(lowPivots, htfPivotLow)
        array.push(lowBroken, false)

if not na(htfPivotHigh) or not na(htfPivotLow)
    lastPivotTime := pivotTime

// Periodic cleanup every 100 bars
if bar_index % 100 == 0
    // Remove broken lines
    if array.size(highLines) > 0
        for i = array.size(highLines) - 1 to 0
            if array.get(highBroken, i)
                line.delete(array.get(highLines, i))
                if showLabels and i < array.size(highLabels)
                    label.delete(array.get(highLabels, i))
                    array.remove(highLabels, i)
                array.remove(highLines, i)
                array.remove(highPivots, i)
                array.remove(highBroken, i)
    
    if array.size(lowLines) > 0
        for i = array.size(lowLines) - 1 to 0
            if array.get(lowBroken, i)
                line.delete(array.get(lowLines, i))
                if showLabels and i < array.size(lowLabels)
                    label.delete(array.get(lowLabels, i))
                    array.remove(lowLabels, i)
                array.remove(lowLines, i)
                array.remove(lowPivots, i)
                array.remove(lowBroken, i)

// Line termination logic
if array.size(highLines) > 0
    for i = array.size(highLines) - 1 to 0
        if not array.get(highBroken, i)
            currentLine = array.get(highLines, i)
            linePrice = array.get(highPivots, i)
            if lineBroken(linePrice, true)
                line.set_x2(currentLine, bar_index)
                array.set(highBroken, i, true)
            else
                line.set_x2(currentLine, bar_index + math.min(pivotLength, 500))

if array.size(lowLines) > 0
    for i = array.size(lowLines) - 1 to 0
        if not array.get(lowBroken, i)
            currentLine = array.get(lowLines, i)
            linePrice = array.get(lowPivots, i)
            if lineBroken(linePrice, false)
                line.set_x2(currentLine, bar_index)
                array.set(lowBroken, i, true)
            else
                line.set_x2(currentLine, bar_index + math.min(pivotLength, 500))

if array.size(highLines) > maxPivots
    line.delete(array.get(highLines, 0))
    if showLabels and array.size(highLabels) > 0
        label.delete(array.get(highLabels, 0))
        array.remove(highLabels, 0)
    array.remove(highLines, 0)
    array.remove(highPivots, 0)
    array.remove(highBroken, 0)

if array.size(lowLines) > maxPivots
    line.delete(array.get(lowLines, 0))
    if showLabels and array.size(lowLabels) > 0
        label.delete(array.get(lowLabels, 0))
        array.remove(lowLabels, 0)
    array.remove(lowLines, 0)
    array.remove(lowPivots, 0)
    array.remove(lowBroken, 0)